varnishtest "BAN "

server s1 {
	rxreq
	txresp -hdr "version: foo_0"

	rxreq
	txresp -hdr "version: bar_0"

	rxreq
	txresp -hdr "version: foo_1"

	rxreq
	txresp -hdr "version: foo_2"

	rxreq
	txresp -hdr "version: bar_1"
} -start

varnish v1 -vcl+backend {
import std;
	include "${pwd}/invalidate_plus.vcl";

	sub vcl_recv {
		# let the request decide
		if (req.http.ignore-host) {
			invalidate_opts.set("ban-ignore-host", "true");
		} else {
			invalidate_opts.set("ban-ignore-host", "false");
		}
		# trust everyone
		invalidate_opts.set("user-authorized", "true");
		call invalidate;
	}
} -start

varnish v1 -cliok "param.set debug +syncvsl"

client c1 {
	txreq -hdr "host: foo.com"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "foo_0"

	txreq -hdr "host: bar.com"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "bar_0"

	# mismatching tag, this should have no effect
	txreq -req RMTAG -hdr "host: baz.com"
	rxresp
	expect resp.status == 200

	txreq -hdr "host: foo.com"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "foo_0"

	txreq -hdr "host: bar.com"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "bar_0"

	# ban foo.com
	txreq -req BAN -hdr "host: foo.com"
	rxresp
	expect resp.status == 200

	# this one will be new
	txreq -hdr "host: foo.com"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "foo_1"

	# but this one is untouched
	txreq -hdr "host: bar.com"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "bar_0"

	# ban everything
	txreq -req BAN -hdr "host: foo.com" -hdr "ignore-host: true"
	rxresp
	expect resp.status == 200

	# this one will be new
	txreq -hdr "host: foo.com"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "foo_2"

	# this one too
	txreq -hdr "host: bar.com"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "bar_1"
} -run
