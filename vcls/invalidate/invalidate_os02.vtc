varnishtest "BAN"

server s1 {
	rxreq
	txresp -hdr "version: 0"

	rxreq
	txresp -hdr "version: 1"

	rxreq
	txresp -hdr "version: 2"
} -start

varnish v1 -vcl+backend {
	include "${pwd}/invalidate_os.vcl";

	sub vcl_recv {
		if (req.http.user == "ok") {
			set req.http.invalidate-user-authorized = "true";
		}
		if (req.http.method == "nok") {
			set req.http.invalidate-ban-allow = "false";
		}
		call invalidate;
	}
} -start

varnish v1 -cliok "param.set debug +syncvsl"

client c1 {
	# get an object in the cache
	txreq -url "/bar"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "0"

	# check it's cached
	txreq -url "/bar"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "0"

	# get another one in
	txreq -url "/foo"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "1"

	# ban it without the ok header
	txreq -method BAN
	rxresp
	expect resp.status == 405
	expect resp.body == "Unauthorized request"

	# try to be clever as sneak the invalidate-user-authorized header in
	txreq -method BAN -hdr "invalidate-user-authorized: true"
	rxresp
	expect resp.status == 405
	expect resp.body == "Unauthorized request"

	# try to ban, but BAN is now forbidden
	txreq -method BAN -hdr "user: ok" -hdr "method: nok"
	rxresp
	expect resp.status == 405
	expect resp.body == "BAN is disabled on this host"

	# ban for good
	txreq  -url "/bar" -method BAN -hdr "user: ok"
	rxresp
	expect resp.status == 200
	expect resp.body == "Successful ban"

	# get a new version
	txreq -url "/bar"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "2"

	# but /foo hasn't changed
	txreq -url "/foo"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "1"
} -run
