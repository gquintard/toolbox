varnishtest "BAN"

server s1 {
	rxreq
	txresp -hdr "version: 0"

	rxreq
	txresp -hdr "version: 1"

	rxreq
	txresp -hdr "version: 2"
} -start

varnish v1 -vcl+backend {
import std;
	include "${pwd}/invalidate_plus.vcl";

	sub vcl_recv {
		if (req.http.user == "ok") {
			invalidate_opts.set("user-authorized", "true");
		}
		if (req.http.method == "nok") {
			invalidate_opts.set("ban-allow", "false");
		}
		call invalidate;
	}
} -start

varnish v1 -cliok "param.set debug +syncvsl"

client c1 {
	# get an object in the cache
	txreq -url "/bar"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "0"

	# check it's cached
	txreq -url "/bar"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "0"

	# get another one in
	txreq -url "/foo"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "1"

	# ban it without the ok header
	txreq -req BAN
	rxresp
	expect resp.status == 405
	expect resp.body == "Unauthorized request"

	# try to be clever as sneak the invalidate-user-authorized header in
	txreq -req BAN -hdr "invalidate-user-authorized: true"
	rxresp
	expect resp.status == 405
	expect resp.body == "Unauthorized request"

	# try to ban, but BAN is now forbidden
	txreq -req BAN -hdr "user: ok" -hdr "method: nok"
	rxresp
	expect resp.status == 405
	expect resp.body == "BAN is disabled on this host"

	# ban for good
	txreq  -url "/bar" -req BAN -hdr "user: ok"
	rxresp
	expect resp.status == 200
	expect resp.body == "Successful ban request"

	# get a new version
	txreq -url "/bar"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "2"

	# but /foo hasn't changed
	txreq -url "/foo"
	rxresp
	expect resp.status == 200
	expect resp.http.version == "1"
} -run
